buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'checkstyle'

apply from: 'project/version-writer.gradle'

sourceCompatibility = 1.7
targetCompatibility = 1.7
tasks.withType(Compile) { options.encoding = 'UTF-8' }

group = "net.awairo.mcmod"
archivesBaseName = "SpawnChecker"
ext {
    modid = "spawnchecker"
}

minecraft {
    version = "1.7.2-10.12.2.1121"

    assetDir = "run/assets"

    replace '@VERSION@', versionInfo.version()
    replace '@MC_VERSION@', minecraft.version
    if (project.hasProperty('signing_fingerprint')) {
        replace '@FINGERPRINT@', project.signing_fingerprint
    } else {
        replace '@FINGERPRINT@', ""
    }
}

repositories {
    mavenCentral()
    maven {
        name = "awairo"
        url = "http://maven.awairo.net/"
    }
}

configurations {
    deployerJars
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
}

processResources {

    inputs.property "version", versionInfo.version()
    inputs.property "mcversion", project.minecraft.version

    exclude 'Log4j-config.xsd', 'Log4j-events.dtd', 'Log4j-events.xsd', 'log4j2.xml'

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':versionInfo.version(), 'modinfoversion':versionInfo.modinfoVersion(), 'mcversion':minecraft.version
    }
}

task deobfJar(type: Jar, dependsOn: classes) {
    classifier = 'dev'
    from sourceSets.main.output
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceMainJava.output
}

javadoc {
    options.encoding = 'utf-8'
    options.locale = 'ja_JP'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task signJar(dependsOn:assemble) << {
    description = "signing jar files"

    if (project.hasProperty('signing_alias') && project.hasProperty('signing_storepass')) {
        logger.info('Signing jar')

        ant.signjar(
            jar: 'build/libs/*.jar',
            alias: project.signing_alias,
            storepass: project.signing_storepass
        )

    } else {
        logger.info('has not singing property')
    }
}

build { dependsOn signJar }

checkstyle {
    configFile = file('project/checkstyle.xml')
    ignoreFailures = true
}
checkstyleMain.excludes = ['net/awairo/mcmod/common/**']
checkstyleTest.excludes = ['**/*']

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

task release(dependsOn:[clean,uploadArchives]) << {
    description 'release '
}

task releaseSnapshot(dependsOn:[clean,uploadArchives]) << {
    description 'snapshot release'
}

gradle.taskGraph.whenReady { taskGraph ->

    if (taskGraph.hasTask(release)) {
        if (versionInfo.beta) {
            println ''
            println '-v-v-v-v-v-v-v-v-v-v-v-v-v-'
            println ''
            println '        BETA VERSION'
            println ''
            println '-v-v-v-v-v-v-v-v-v-v-v-v-v-'
            println ''
            println 'stop :release task...'

            throw new StopExecutionException("beta version")
        }

        logger.quiet('>>>RELEASE<<<')

        revisionUp()
        uploadArchives.repositories.mavenDeployer.pom.version = project.version = versionInfo.version()

    } else if (taskGraph.hasTask(releaseSnapshot)) {
        logger.quiet('>>>SNAPSHOT RELEASE<<<')

        writeVersion()
        uploadArchives.repositories.mavenDeployer.pom.version = project.version = versionInfo.snapshotVersion()

    } else {
        writeVersion()
        uploadArchives.repositories.mavenDeployer.pom.version = project.version = versionInfo.version()

    // update configuration of source copy tasks
    project.tasks.withType(net.minecraftforge.gradle.tasks.user.SourceCopyTask.class).each {
        t -> t.replace '@VERSION@', project.version
    }
    project.tasks.findAll { t -> t.name.equals("processResources") }.each { t ->
        t.from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            expand 'version':project.version, 'modinfoversion':versionInfo.modinfoVersion(), 'mcversion':minecraft.version
        }
    }

    logger.quiet('upload archives version: ' + version)
}

apply from: 'project/publish.gradle'
